definition {

	@summary = "Activate local staging with or without page versioning with all content"
	macro enableLocalStaging {
		Variables.assertDefined(parameterList = "${groupName}");

		if (!(isSet(globalSite))) {
			var groupId = JSONGroupAPI._getSiteIdByGroupKey(groupName = "${groupName}");
		}

		if (isSet(globalSite)) {
			var companyId = JSONCompany.getCompanyId();

			var groupId = JSONGroupAPI._getSiteIdByGroupKey(groupName = "${companyId}");
		}

		if (!(isSet(branchingPrivate))) {
			var branchingPrivate = "false";
		}

		if (!(isSet(branchingPublic))) {
			var branchingPublic = "false";
		}

		JSONStagingAPI._enableLocalStaging(
			branchingPrivate = "${branchingPrivate}",
			branchingPublic = "${branchingPublic}",
			groupId = "${groupId}");

		Pause(locator1 = "3000");
	}

	@summary = "Activate remote staging with or without page versioning with all content"
	macro enableRemoteStaging {
		Variables.assertDefined(parameterList = "${groupName},${remoteGroupId}");

		var groupId = JSONGroupAPI._getSiteIdByGroupKey(groupName = "${groupName}");

		if (!(isSet(branchingPrivate))) {
			var branchingPrivate = "false";
		}

		if (!(isSet(branchingPublic))) {
			var branchingPublic = "false";
		}

		JSONStagingAPI._enableRemoteStaging(
			branchingPrivate = "${branchingPrivate}",
			branchingPublic = "${branchingPublic}",
			groupId = "${groupId}",
			remoteGroupId = "${remoteGroupId}",
			remotePort = "${remotePort}");
	}

}